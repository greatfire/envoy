apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {

    // exclude lint task from demo project that causes release builds to fail
    if (gradle.startParameter.taskNames.contains("assembleRelease")) {
        gradle.startParameter.excludedTaskNames += ":demo:lintVitalRelease"
    }

    compileSdkVersion 29
    buildToolsVersion '30.0.2'
    //ndkVersion '20.0.5594570'
    ndkVersion '21.0.6113669'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName '1.0'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs = ['src/main/kotlin', 'src/test/kotlin']
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    // implementation fileTree(dir: 'libs', include: ['*.jar'])
    // implementation 'androidx.appcompat:appcompat:1.1.0'
    //testImplementation 'junit:junit:4.12'
    //androidTestImplementation 'androidx.test:runner:1.1.1'
    //androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'

    implementation('com.squareup.okhttp3:okhttp:4.6.0')
    implementation('com.squareup.okhttp3:okhttp-urlconnection:4.4.1')
    //noinspection GradleDependency
    //implementation('com.squareup.okhttp3:okhttp:3.14.8')
    //implementation('com.squareup.okhttp3:okhttp-urlconnection:3.14.8')

    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.4.32'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'org.greatfire.envoy:cronet:102.0.5005.195-4'
    implementation 'org.greatfire:IEnvoyProxy:1.2.1'

    // debugApi(name: 'cronet-debug', ext: 'aar')
    // releaseApi(name: 'cronet-release', ext: 'aar')
}

def stdout = new ByteArrayOutputStream()
exec {
    commandLine 'git', 'describe', '--tags', '--always'
    standardOutput = stdout
}
version = stdout.toString().trim()
group = 'org.greatfire'

task sourcesZip {
  def src = 'https://github.com/greatfire/envoy/archive/refs/tags/' + project.version + '.zip'
  def destdir = project.getBuildDir().toString() + "/outputs/aar"
  def destfile = "$destdir/envoy-" + project.version + "-sources.zip"
  doLast {
    def url = new URL(src)
    def f = new File(destfile)
    if (f.exists()) {
      println "file $destfile already exists, skipping download"
    } else {
      mkdir "$destdir"
      println "Downloading $destfile from $url..."
      url.withInputStream { i -> f.withOutputStream { it << i } }
    }
  }
}

apply plugin: 'maven'

task writeNewPom {
    pom {
        project {
            name 'Envoy'
	    packaging 'aar'
            artifactId 'envoy'
            inceptionYear '2018'
            url 'https://github.com/greatfire/envoy'
	    description 'C and Java Library derived from Chromium cronet'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'https://github.com/greatfire/envoy/blob/master/LICENSE'
                    distribution 'repo'
                }
		license {
		    name 'BSD-3-clause'
		    url 'https://chromium.googlesource.com/chromium/src/+/refs/heads/main/LICENSE'
		    distribution 'repo'
		}
            }
            developers {
                developer {
                    id = 'greatfire'
                    name = 'GreatFire'
                    email = 'support@greatfire.org'
                }
                developer {
                    id = 'guardianproject'
                    name = 'Guardian Project'
                    email = 'support@guardianproject.info'
                }
            }
            issueManagement {
                url = "https://github.com/greatfire/envoy/issues"
		system = "GitHub"
            }
            scm {
                connection = 'scm:git:https://github.com/greatfire/envoy.git'
                url = 'https://github.com/greatfire/envoy'
            }
        }
    }.writeTo(project.getBuildDir().toString() + "/outputs/aar/" + "envoy" + "-" + project.version + ".pom")
}
